/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=rzx); // result zero x
    Mux16(a=y, b=false, sel=zy, out=rzy); // result zero y

    Not16(in=rzx, out=nrzx); // !result zero x
    Not16(in=rzy, out=nrzy); // !result zero y

    Mux16(a=rzx, b=nrzx, sel=nx, out=rnx); // result !x
    Mux16(a=rzy, b=nrzy, sel=ny, out=rny); // result !y

    Add16(a=rnx, b=rny, out=xpy); // x plus y
    And16(a=rnx, b=rny, out=xay); // x and y
    Mux16(a=xay, b=xpy, sel=f, out=xy); // xpy or xay = xy, fuck it

    Not16(in=xy, out=nxy);
    Mux16(a=xy, b=nxy, sel=no, out=out, out[15]=ng, out[0..7]=lsb, out[8..15]=msb);

    // Just check if any bit is 1, if so, carry it all over.
    Or8Way(in=lsb, out=zl); // lsb = least significative byte ~ zero 'least'
    Or8Way(in=msb, out=zm); // most significative byte ~ zero 'most'
    Or(a=zl, b=zm, out=nz); // or both
    Not(in=nz, out=zr);
}
